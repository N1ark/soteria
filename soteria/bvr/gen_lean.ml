(** Lean backend.

    Functions with a [[@spec]] are rule functions. Each of their rules becomes a
    Lean function [f.r.name : Ops -> args -> Option Term] (the rule's patterns
    and guard, returning its right-hand side), in which the calls to rule
    functions go through the record [O : Ops]. [f.step O] tries the rules in
    order and falls back to the spec; [opsN orc n] ties the knot with [n] steps
    of fuel (fuel 0 being the raw specs).

    Helpers that call rule functions or oracles take [O] as a parameter; the
    other helpers are plain Lean functions.

    Three files are generated: [Model.lean] (the above), [Statements.lean]
    (soundness of [Ops], and one statement per rule) and [Soundness.lean]
    (soundness of [opsN], from the proofs [f.r.name.proof] of the rule
    statements, written by hand). *)

open Syntax

let pf = Format.fprintf

let list ?(sep = ", ") pp ft l =
  Format.pp_print_list ~pp_sep:(fun ft () -> pf ft "%s" sep) pp ft l

let keywords =
  [
    "by";
    "at";
    "fun";
    "from";
    "have";
    "show";
    "let";
    "if";
    "then";
    "else";
    "do";
    "match";
    "with";
    "in";
    "end";
    "open";
    "theorem";
    "def";
    "where";
    "deriving";
    "instance";
    "structure";
    "class";
    "example";
    "calc";
    "mut";
    "return";
    "for";
    "unless";
    "import";
    "section";
    "namespace";
    "variable";
    "universe";
    "private";
    "protected";
    "partial";
    "noncomputable";
    "mutual";
    "inductive";
    "extends";
    "abbrev";
    "macro";
    "syntax";
    "notation";
    "local";
    "attribute";
    "Type";
    "Prop";
    "Sort";
    "at";
    "obtain";
    "using";
    "fin";
  ]

let id x = if List.mem x keywords then "«" ^ x ^ "»" else x

(* ---------------------------------------------------------------- *)
(* Classification *)

type kind = Rule | OHelper | Pure
type ctx = { prims : prim list; kinds : (string * kind) list; fns : fn list }

let fn_kind ctx f = List.assoc f ctx.kinds
let is_oracle ctx f = List.exists (fun p -> p.pname = f && p.oracle) ctx.prims
let is_prim ctx f = List.exists (fun p -> p.pname = f) ctx.prims

let classify (p : program) =
  let is_oracle f = List.exists (fun q -> q.pname = f && q.oracle) p.prims in
  let kinds =
    ref
      (List.map
         (fun f -> (f.name, if Option.is_some f.spec then Rule else Pure))
         p.fns)
  in
  let changed = ref true in
  while !changed do
    changed := false;
    List.iter
      (fun f ->
        if List.assoc f.name !kinds = Pure then
          let needs_o =
            List.exists
              (fun g ->
                is_oracle g
                ||
                match List.assoc_opt g !kinds with
                | Some (Rule | OHelper) -> true
                | _ -> false)
              (Gen_ocaml.calls [] f.body)
          in
          if needs_o then (
            kinds := (f.name, OHelper) :: List.remove_assoc f.name !kinds;
            changed := true))
      p.fns
  done;
  { prims = p.prims; kinds = !kinds; fns = p.fns }

(* ---------------------------------------------------------------- *)
(* Types *)

let rec lean_ty ft = function
  | TInt -> pf ft "Int"
  | TBv -> pf ft "BvVal"
  | TBool -> pf ft "Bool"
  | TUnit -> pf ft "Unit"
  | TTerm -> pf ft "Term"
  | TKind -> pf ft "Kind"
  | TSty -> pf ft "Ty"
  | TFloat -> pf ft "FloatLit"
  | TVar -> pf ft "Int"
  | TData "unop" -> pf ft "Unop"
  | TData "binop" -> pf ft "Binop"
  | TData "triop" -> pf ft "Triop"
  | TData "nop" -> pf ft "Nop"
  | TData "checked" -> pf ft "Checked"
  | TData "rm" -> pf ft "RM"
  | TData "fp" -> pf ft "Prec"
  | TData "fc" -> pf ft "FClass"
  | TData "ext" -> pf ft "Ext"
  | TData "ext_ty" -> pf ft "ExtTy"
  | TData s -> failwith ("lean_ty: " ^ s)
  | TTuple l -> pf ft "(%a)" (list ~sep:" × " lean_ty) l
  | TOption t -> pf ft "(Option %a)" lean_ty t
  | TList t -> pf ft "(List %a)" lean_ty t

(* ---------------------------------------------------------------- *)
(* Patterns *)

let rec pat ft (p : pat) =
  match p.p with
  | PAny -> pf ft "_"
  | PVar x -> pf ft "%s" (id x)
  | PLit x -> pf ft "%s@(Term.mk (Kind.bitVec _) _)" (id x)
  | PAs (q, x) -> pf ft "%s@%a" (id x) pat q
  | POr _ | PComm _ -> failwith "gen_lean: or-pattern after desugaring"
  | PInt z -> pf ft "(%s : Int)" (Z.to_string z)
  | PBool b -> pf ft "%b" b
  | PUnit -> pf ft "()"
  | PTuple l -> pf ft "(%a)" (list pat) l
  | PSome q -> pf ft "(some %a)" pat q
  | PNone -> pf ft "none"
  | PNil -> pf ft "[]"
  | PCons (h, t) -> pf ft "(%a :: %a)" pat h pat t
  | PRecord fs ->
      let field f =
        match List.assoc_opt f fs with
        | Some p -> Fmt.str "%a" pat p
        | None -> "_"
      in
      pf ft "⟨%s, %s⟩" (field "signed") (field "unsigned")
  | PConstr (c, args) ->
      let inner ft () =
        match args with
        | [] -> pf ft "%s" c.c_lean
        | _ -> pf ft "(%s %a)" c.c_lean (list ~sep:" " pat) args
      in
      if p.pty = TTerm then pf ft "(Term.mk %a _)" inner () else inner ft ()

(* ---------------------------------------------------------------- *)
(* Expressions *)

(** Variables printed as given terms: the scrutinees and [as]/[PLit] binders of
    a [[@cases]] alternative, in its statement. *)
let subst : (string * string) list ref = ref []

(** [k ()] with the variables [xs] bound, hence not substituted. *)
let binding xs k =
  let saved = !subst in
  subst := List.filter (fun (x, _) -> not (List.mem x xs)) saved;
  Fun.protect ~finally:(fun () -> subst := saved) k

(** Whether the rule being printed belongs to a [[@cases]] function. *)
let cases_style = ref false

let rec expr ctx ft (e : expr) =
  let expr = expr ctx in
  match e.e with
  | EVar x -> (
      match List.assoc_opt x !subst with
      | Some t -> pf ft "%s" t
      | None -> pf ft "%s" (id x))
  | EInt z -> pf ft "(%s : Int)" (Z.to_string z)
  | EBool b -> pf ft "%b" b
  | EUnit -> pf ft "()"
  | EConstr (c, []) -> pf ft "%s" c.c_lean
  | EConstr (c, args) -> pf ft "(%s %a)" c.c_lean (list ~sep:" " expr) args
  | ENode (k, t) -> pf ft "(Term.mk %a %a)" expr k expr t
  | ECall (f, args) ->
      let f =
        if is_oracle ctx f then "O.orc." ^ f
        else if is_prim ctx f then f
        else
          match fn_kind ctx f with
          | Rule -> "O." ^ f
          | OHelper -> f ^ " O"
          | Pure -> f
      in
      if args = [] then pf ft "%s" f
      else pf ft "(%s %a)" f (list ~sep:" " expr) args
  | ELocalCall (f, args) -> pf ft "(%s %a)" (id f) (list ~sep:" " expr) args
  | EUnop (Neg, a) -> pf ft "(- %a)" expr a
  | EUnop (Not, a) -> pf ft "(! %a)" expr a
  | EUnop (Lognot, a) -> pf ft "(zlognot %a)" expr a
  | EBinop (Arith op, a, b) -> (
      let infix s = pf ft "(%a %s %a)" expr a s expr b in
      let dec s = pf ft "(decide (%a %s %a))" expr a s expr b in
      match op with
      | Add -> infix "+"
      | Sub -> infix "-"
      | Mul -> infix "*"
      | Lt -> dec "<"
      | Le -> dec "≤"
      | Gt -> dec ">"
      | Ge -> dec "≥"
      | Eq -> dec "="
      | Ne -> dec "≠"
      | And -> infix "&&"
      | Or -> infix "||")
  | EBinop (Bit op, a, b) ->
      let f =
        match op with
        | Land -> "zland"
        | Lor -> "zlor"
        | Lxor -> "zlxor"
        | Lsl -> "zshiftl"
        | Asr -> "zasr"
      in
      pf ft "(%s %a %a)" f expr a expr b
  | EIf (c, a, b) ->
      pf ft "@[<hv>(if %a@ then %a@ else %a)@]" expr c expr a expr b
  | ELet ({ p = PVar x; _ }, rhs, body) ->
      pf ft "@[<v>(let %s := %a;@ %a)@]" (id x) expr rhs
        (fun ft () -> binding [ x ] (fun () -> expr ft body))
        ()
  | ELet (p, rhs, body) ->
      pf ft "@[<v>(match %a with@ | %a =>@;<1 2>%a)@]" expr rhs pat p
        (fun ft () -> binding (pat_names p) (fun () -> expr ft body))
        ()
  | ELetFun (f, params, fbody, body) ->
      pf ft "@[<v>(let %s := fun %a =>@;<1 2>%a;@ %a)@]" (id f)
        (list ~sep:" " (fun ft (x, t) -> pf ft "(%s : %a)" (id x) lean_ty t))
        params
        (fun ft () ->
          binding (f :: List.map fst params) (fun () -> expr ft fbody))
        ()
        (fun ft () -> binding [ f ] (fun () -> expr ft body))
        ()
  | EMatch (scruts, cases) -> match_ ctx ft (scruts, cases)
  | ETuple l -> pf ft "(%a)" (list expr) l
  | ESome e -> pf ft "(some %a)" expr e
  | ENone -> pf ft "none"
  | ENil -> pf ft "[]"
  | ECons (h, t) -> pf ft "(%a :: %a)" expr h expr t
  | ERecord fs ->
      pf ft "({ signed := %a, unsigned := %a } : Checked)" expr
        (List.assoc "signed" fs) expr (List.assoc "unsigned" fs)
  | EField (e, f) -> pf ft "%a.%s" expr e f
  | EAssert (_, body) -> expr ft body

and pat_names p = List.map fst (Check.binders p)

(** Rebinds the variables of [PLit] patterns to their values. *)
and lit_lets ft (p : pat) =
  List.iter
    (fun x -> pf ft "let %s := bv_of_lit %s;@ " (id x) (id x))
    (Check.lit_binders p)

(** Prints [k] after the [lit_lets] of [p], if any. *)
and with_lits p k ft () =
  if Check.lit_binders p = [] then k ft ()
  else pf ft "@[<hv>(%a%a)@]" lit_lets p k ()

(** A case's result: [some body], under its guard. *)
and guarded ctx (c : case) ft () =
  binding (pat_names c.pat) (fun () ->
      with_lits c.pat
        (fun ft () ->
          match c.guard with
          | None when !cases_style ->
              pf ft "@[<hv>(whenSome true@ (%a))@]" (expr ctx) c.body
          | None -> pf ft "some (%a)" (expr ctx) c.body
          | Some g when !cases_style ->
              pf ft "@[<hv>(whenSome %a@ (%a))@]" (expr ctx) g (expr ctx) c.body
          | Some g ->
              pf ft "@[<hv>(if %a@ then some (%a)@ else none)@]" (expr ctx) g
                (expr ctx) c.body)
        ft ())

(** Whether a pattern matches every value of its type. *)
and irrefutable (p : pat) =
  match p.p with
  | PAny | PVar _ | PUnit -> true
  | PAs (q, _) -> irrefutable q
  | PTuple l -> List.for_all irrefutable l
  | PRecord fs -> List.for_all (fun (_, q) -> irrefutable q) fs
  | _ -> false

(** How a match on [scruts] is printed: on several discriminants when possible
    (so that Lean sees the recursion on each of them), else on a tuple. *)
and discriminants ctx (scruts, (cases : case list)) =
  let multi =
    List.length scruts > 1
    && List.for_all
         (fun (c : case) ->
           match c.pat.p with PTuple _ | PAny -> true | _ -> false)
         cases
  in
  let d ft () =
    if multi || List.length scruts = 1 then list (expr ctx) ft scruts
    else pf ft "(%a)" (list (expr ctx)) scruts
  in
  let p ft (q : pat) =
    match q.p with
    | PTuple l when multi -> list pat ft l
    | PAny when multi -> list (fun ft _ -> pf ft "_") ft scruts
    | _ -> pat ft q
  in
  let wild ft () =
    if multi then list (fun ft _ -> pf ft "_") ft scruts else pf ft "_"
  in
  (d, p, wild)

(** A match with guards: each case is a Lean match of its own, returning [none]
    when its pattern or guard fails, and the first case that applies gives the
    result. A last case that always applies is the default. *)
and match_ ctx ft (scruts, cases) =
  let d, p, wild = discriminants ctx (scruts, cases) in
  let alt ft (c : case) =
    let rhs = guarded ctx c in
    if irrefutable c.pat then
      pf ft "@[<hv 2>(match %a with@ | %a =>@ %a)@]" d () p c.pat rhs ()
    else
      pf ft "@[<hv 2>(match %a with@ | %a =>@ %a@ | %a => none)@]" d () p c.pat
        rhs () wild ()
  in
  let alts, default =
    match List.rev cases with
    | ({ guard = None; _ } as c) :: rest when irrefutable c.pat ->
        ( List.rev rest,
          fun ft () ->
            pf ft "@[<hv 2>(match %a with@ | %a =>@ %a)@]" d () p c.pat
              (fun ft () ->
                binding (pat_names c.pat) (fun () ->
                    with_lits c.pat (fun ft () -> expr ctx ft c.body) ft ()))
              () )
    | _ -> (cases, fun ft () -> pf ft "Inhabited.default")
  in
  match alts with
  | [] -> default ft ()
  | _ ->
      pf ft "@[<hv 2>((firstSome [%a]).getD@ %a)@]"
        (Format.pp_print_list ~pp_sep:(fun ft () -> pf ft ",@ ") alt)
        alts default ()

(* ---------------------------------------------------------------- *)
(* Rules *)

(** A rule function's body: a prefix of [let]s and [assert]s, then a match whose
    consecutive cases with the same name form the rules. *)
let rec split_body (e : expr) : (expr -> expr) * expr list * case list list =
  match e.e with
  | ELet (p, rhs, body) ->
      let pre, s, r = split_body body in
      ((fun x -> { e with e = ELet (p, rhs, pre x) }), s, r)
  | EAssert (_, body) -> split_body body
  | EMatch (scruts, cases) ->
      let groups =
        List.fold_left
          (fun acc (c : case) ->
            match acc with
            | (g :: _ as grp) :: rest when g.rule = c.rule ->
                (grp @ [ c ]) :: rest
            | _ -> [ c ] :: acc)
          [] cases
        |> List.rev
      in
      ((fun x -> x), scruts, groups)
  | _ ->
      ( (fun x -> x),
        [],
        [
          [
            {
              pat = { p = PAny; pty = TUnit; ploc = e.eloc; pid = 0 };
              guard = None;
              body = e;
              rule = Some "main";
              cloc = e.eloc;
              alt = [];
            };
          ];
        ] )

let rule_name (f : fn) (grp : case list) =
  match (List.hd grp).rule with
  | Some r -> r
  | None ->
      raise
        (Check.Error ((List.hd grp).cloc, Fmt.str "%s: unnamed rule" f.name))

let params ft (f : fn) =
  list ~sep:" "
    (fun ft (x, t) -> pf ft "(%s : %a)" (id x) lean_ty t)
    ft f.params

let args ft (f : fn) =
  list ~sep:" " (fun ft (x, _) -> pf ft "%s" (id x)) ft f.params

let arrow ft (f : fn) =
  List.iter (fun (_, t) -> pf ft "%a → " lean_ty t) f.params;
  lean_ty ft f.ret

(** The Lean function for one rule. *)
let rule_def ctx ft (f : fn) (pre, scruts, grp) =
  cases_style := f.cases;
  Fun.protect ~finally:(fun () -> cases_style := false) @@ fun () ->
  let name = rule_name f grp in
  let d, p, wild = discriminants ctx (scruts, grp) in
  let alt ft (c : case) =
    let rhs = guarded ctx c in
    if scruts = [] then rhs ft ()
    else if irrefutable c.pat then
      pf ft "@[<hv 2>(match %a with@ | %a =>@ %a)@]" d () p c.pat rhs ()
    else
      pf ft "@[<hv 2>(match %a with@ | %a =>@ %a@ | %a => none)@]" d () p c.pat
        rhs () wild ()
  in
  let rec lets ft (e : expr) =
    match e.e with
    | ELet ({ p = PVar x; _ }, rhs, b) ->
        pf ft "let %s := %a;@ %a" (id x) (expr ctx) rhs lets b
    | ELet _ -> failwith "gen_lean: destructuring let before a rule match"
    | _ -> Format.pp_print_list ~pp_sep:(fun ft () -> pf ft "@ <|> ") alt ft grp
  in
  pf ft "@[<v 2>def %s.r_%s (O : Ops) %a : Option Term :=@ %a@]@ @ " f.name
    (id name) params f lets
    (pre { (List.hd grp).body with e = EUnit })

let rules (f : fn) =
  let pre, scruts, groups = split_body f.body in
  List.map (fun g -> (pre, scruts, g)) groups

(* ---------------------------------------------------------------- *)
(* [[@cases]]: one statement per alternative ("arm") of a rule *)

(** An arm's statement: its binders (name, Lean type), the substitution of the
    scrutinees and pattern aliases, and for each substituted name the [pid] of
    the pattern it stands for. *)
type arm = {
  a_case : case;
  a_binders : (string * string) list;
  a_subst : (string * (string * int)) list;
  a_body_subst : (string * string) list;
      (** [a_subst] without the names that the pattern rebinds *)
}

let ty_str t = Fmt.str "%a" lean_ty t

(** The term a pattern matches, with its wildcards named after their [pid]. *)
let pat_term (p : pat) =
  let binders = ref [] and subst = ref [] in
  let bind x t = binders := !binders @ [ (x, t) ] in
  let rec go (p : pat) =
    match p.p with
    | PAny ->
        let x = Printf.sprintf "w__%d" p.pid in
        bind x (ty_str p.pty);
        x
    | PVar x ->
        bind (id x) (ty_str p.pty);
        id x
    | PLit x ->
        let z = x ^ "__z" and t = x ^ "__T" in
        bind z "Int";
        bind t "Ty";
        let term = Printf.sprintf "(Term.mk (Kind.bitVec %s) %s)" z t in
        subst := (x, (Printf.sprintf "(bv_of_lit %s)" term, p.pid)) :: !subst;
        term
    | PAs (q, x) ->
        let t = go q in
        subst := (x, (t, q.pid)) :: !subst;
        t
    | PInt z -> Printf.sprintf "(%s : Int)" (Z.to_string z)
    | PBool b -> string_of_bool b
    | PUnit -> "()"
    | PTuple l -> "(" ^ String.concat ", " (List.map go l) ^ ")"
    | PSome q -> "(some " ^ go q ^ ")"
    | PNone -> "none"
    | PNil -> "[]"
    | PCons (h, t) ->
        let h = go h in
        "(" ^ h ^ " :: " ^ go t ^ ")"
    | PRecord fs ->
        let field f =
          match List.assoc_opt f fs with
          | Some q -> go q
          | None ->
              let x = Printf.sprintf "%s__%d" f p.pid in
              bind x "Bool";
              x
        in
        let sg = field "signed" in
        "⟨" ^ sg ^ ", " ^ field "unsigned" ^ "⟩"
    | PConstr (c, args) ->
        let args = List.map go args in
        let inner =
          if args = [] then c.c_lean
          else "(" ^ String.concat " " (c.c_lean :: args) ^ ")"
        in
        if p.pty = TTerm then (
          let t = Printf.sprintf "t__%d" p.pid in
          bind t "Ty";
          "(Term.mk " ^ inner ^ " " ^ t ^ ")")
        else inner
    | POr _ | PComm _ -> failwith "gen_lean: or-pattern after desugaring"
  in
  let t = go p in
  (t, !binders, !subst)

let cases_error (f : fn) loc fmt =
  Fmt.kstr (fun s -> raise (Check.Error (loc, f.name ^ ": " ^ s))) fmt

let arm_of (f : fn) scruts (c : case) : arm =
  let scrut_params =
    List.map
      (fun (e : expr) ->
        match e.e with
        | EVar x when List.mem_assoc x f.params -> x
        | _ -> cases_error f e.eloc "[@cases] matches on parameters only")
      scruts
  in
  let pats =
    match (scrut_params, c.pat.p) with
    | [ _ ], _ -> [ c.pat ]
    | _, PTuple l -> l
    | _, PAny -> List.map (fun _ -> c.pat) scrut_params
    | _ -> cases_error f c.cloc "unexpected pattern"
  in
  let substituted = ref [] and binders = ref [] and subst = ref [] in
  List.iter2
    (fun x (p : pat) ->
      match p.p with
      | PAny -> ()
      | PVar y -> subst := (y, (id x, p.pid)) :: !subst
      | _ ->
          let t, bs, sb = pat_term p in
          substituted := x :: !substituted;
          binders := !binders @ bs;
          subst := ((x, (t, p.pid)) :: sb) @ !subst)
    scrut_params pats;
  let params =
    List.filter_map
      (fun (x, t) ->
        if List.mem x !substituted then None else Some (id x, ty_str t))
      f.params
  in
  List.iter
    (fun (x, _) ->
      if List.mem_assoc x params then
        cases_error f c.cloc "pattern variable %s shadows a parameter" x)
    !binders;
  (* in the guard and body, pattern variables shadow the scrutinees they are
     part of *)
  let names = List.map fst !binders in
  let body_subst =
    List.filter_map
      (fun (x, (t, _)) -> if List.mem (id x) names then None else Some (x, t))
      !subst
  in
  {
    a_case = c;
    a_binders = params @ !binders;
    a_subst = !subst;
    a_body_subst = body_subst;
  }

(** Arms of a rule function, per rule. *)
let arms (f : fn) =
  List.map
    (fun (pre, scruts, grp) ->
      if (pre { f.body with e = EUnit }).e <> EUnit then
        cases_error f f.floc "[@cases]: no let before the match";
      (rule_name f grp, List.map (arm_of f scruts) grp))
    (rules f)

(** Whether [x] occurs in [e] other than as the argument of [ty] or [size] (when
    [ty_ok]), where it is not rebound. *)
let rec occurs ~ty_ok x (e : expr) =
  let go = occurs ~ty_ok x in
  match e.e with
  | EVar y -> x = y
  | ECall (("ty" | "size"), [ { e = EVar y; _ } ]) when y = x -> not ty_ok
  | EInt _ | EBool _ | EUnit | ENone | ENil -> false
  | ECall (_, l) | EConstr (_, l) | ELocalCall (_, l) | ETuple l ->
      List.exists go l
  | ENode (a, b) | EBinop (_, a, b) | ECons (a, b) | EAssert (a, b) ->
      go a || go b
  | EUnop (_, a) | ESome a | EField (a, _) -> go a
  | EIf (a, b, c) -> go a || go b || go c
  | ERecord l -> List.exists (fun (_, e) -> go e) l
  | ELet (p, a, b) -> go a || ((not (List.mem x (pat_names p))) && go b)
  | ELetFun (g, ps, a, b) ->
      ((not (List.mem x (g :: List.map fst ps))) && go a) || (g <> x && go b)
  | EMatch (scruts, cases) ->
      List.exists go scruts
      || List.exists
           (fun (c : case) ->
             (not (List.mem x (pat_names c.pat)))
             && (Option.fold ~none:false ~some:go c.guard || go c.body))
           cases

(** The arm that the arm [a] is derived from by commutativity, if any: the one
    of the same source case that takes the same or-pattern choices and no
    [[@comm]] swap, when [a]'s guard and body do not depend on the swaps. *)
let derived_from (grp : arm list) (a : arm) =
  let c = a.a_case in
  if not (List.exists (fun (_, i, comm) -> comm && i = 1) c.alt) then None
  else
    let norm l = List.sort compare l in
    let base_alt =
      norm
        (List.map
           (fun (p, i, comm) -> (p, (if comm then 0 else i), comm))
           c.alt)
    in
    match
      List.find_opt
        (fun b -> b.a_case.cloc = c.cloc && norm b.a_case.alt = base_alt)
        grp
    with
    | None -> None
    | Some b ->
        let names =
          List.sort_uniq compare
            (List.map fst a.a_subst @ List.map fst b.a_subst)
        in
        let independent x =
          match (List.assoc_opt x a.a_subst, List.assoc_opt x b.a_subst) with
          | Some (t, _), Some (t', _) when t = t' -> true
          | Some (_, p), Some (_, p') ->
              let ty_ok = p = p' in
              let e = c.body and g = c.guard in
              not
                (occurs ~ty_ok x e
                || Option.fold ~none:false ~some:(occurs ~ty_ok x) g)
          | _ -> false
        in
        if List.for_all independent names then Some b else None

(* ---------------------------------------------------------------- *)
(* Files *)

let header ~sources ft imports =
  pf ft "@[<v>-- Generated by bvr from %a. Do not edit.@ "
    (list Format.pp_print_string)
    sources;
  List.iter (fun i -> pf ft "import %s@ " i) imports;
  pf ft
    "@ set_option linter.unusedVariables false@ set_option maxHeartbeats \
     1000000@ @ noncomputable section@ @ namespace Bvr@ @ open Classical@ @ "

(** The parameter a recursive helper recurses on: the first variable its body
    matches on. *)
let rec decreasing (f : fn) (e : expr) =
  match e.e with
  | ELet (_, _, b) | EAssert (_, b) | ELetFun (_, _, _, b) -> decreasing f b
  | EMatch (scruts, _) ->
      List.find_map
        (fun (s : expr) ->
          match s.e with
          | EVar x when List.mem_assoc x f.params -> Some x
          | _ -> None)
        scruts
  | _ -> None

let fn_def ctx ft (f : fn) ~o ~kw ~recursive =
  pf ft "@[<v 2>%s %s%s %a : %a :=@ %a@]@ " kw (id f.name)
    (if o then " (O : Ops)" else "")
    params f lean_ty f.ret (expr ctx) f.body;
  (if recursive then
     match decreasing f f.body with
     | Some x ->
         pf ft
           "termination_by sizeOf %s@ decreasing_by all_goals (simp_wf; \
            omega)@ "
           (id x)
     | None -> ());
  pf ft "@ "

let defs ctx ft kind ~o =
  List.iter
    (fun group ->
      let group = List.filter (fun f -> fn_kind ctx f.name = kind) group in
      match group with
      | [] -> ()
      | [ f ] when not (Gen_ocaml.is_recursive [ f ]) ->
          fn_def ctx ft f ~o ~kw:"def" ~recursive:false
      | _ ->
          pf ft "mutual@ @ ";
          List.iter (fn_def ctx ft ~o ~kw:"def" ~recursive:true) group;
          pf ft "end@ @ ")
    (Gen_ocaml.sccs ctx.fns)

let rule_fns ctx = List.filter (fun f -> fn_kind ctx f.name = Rule) ctx.fns

let model ~sources ft (p : program) =
  let ctx = classify p in
  header ~sources ft [ "Bvr.Prims" ];
  (* oracles *)
  pf ft
    "@[<v 2>/-- The primitives that the model is parameterised by. -/@ \
     structure Oracle where";
  List.iter
    (fun (q : prim) ->
      if q.oracle then (
        pf ft "@ %s : " q.pname;
        List.iter (fun t -> pf ft "%a → " lean_ty t) q.pargs;
        lean_ty ft q.pret))
    p.prims;
  pf ft "@]@ @ ";
  defs ctx ft Pure ~o:false;
  (* Ops *)
  pf ft
    "@[<v 2>/-- The rule functions, as used by the rules. -/@ structure Ops \
     where@ orc : Oracle";
  List.iter (fun f -> pf ft "@ %s : %a" f.name arrow f) (rule_fns ctx);
  pf ft "@]@ @ ";
  defs ctx ft OHelper ~o:true;
  (* specs *)
  List.iter
    (fun f ->
      pf ft "@[<v 2>@@[bvr_spec] def %s.spec %a : Term :=@ %a@]@ @ " f.name
        params f (expr ctx) (Option.get f.spec))
    (rule_fns ctx);
  (* rules and steps *)
  List.iter
    (fun f ->
      let rs = rules f in
      List.iter (rule_def ctx ft f) rs;
      pf ft
        "@[<v 2>def %s.step (O : Ops) %a : Term :=@ (firstSome [%a]).getD \
         (%s.spec %a)@]@ @ "
        f.name params f
        (list (fun ft r ->
             pf ft "%s.r_%s O %a" f.name
               (id
                  (rule_name f
                     (let _, _, g = r in
                      g)))
               args f))
        rs f.name args f)
    (rule_fns ctx);
  let fields ft mk =
    List.iter (fun f -> pf ft ",\n    %s := %a" f.name mk f) (rule_fns ctx)
  in
  pf ft "@[<v 2>def opsRaw (orc : Oracle) : Ops :=@ { orc := orc%a }@]@ @ "
    fields (fun ft f -> pf ft "fun %a => %s.spec %a" args f f.name args f);
  pf ft "@[<v 2>def opsStep (O : Ops) : Ops :=@ { orc := O.orc%a }@]@ @ " fields
    (fun ft f -> pf ft "%s.step O" f.name);
  pf ft
    "@[<v 2>def opsN (orc : Oracle) : Nat → Ops@ | 0 => opsRaw orc@ | n + 1 => \
     opsStep (opsN orc n)@]@ @ ";
  pf ft "end Bvr@]@."

let arm_name f r i = Printf.sprintf "%s.r_%s.a%d" f.name (id r) (i + 1)

(** The statement of an arm: under its guard, the spec at the matched arguments
    is refined by the body. *)
let arm_stmt ctx ft f r i (a : arm) =
  let c = a.a_case in
  let with_subst s k =
    subst := s;
    Fun.protect ~finally:(fun () -> subst := []) k
  in
  pf ft
    "@[<v 2>def %s.Stmt : Prop :=@ ∀ (FS : FloatSem) (O : Ops), O.Sound FS →@ "
    (arm_name f r i);
  pf ft "∀ %a,@ "
    (list ~sep:" " (fun ft (x, t) -> pf ft "(%s : %s)" x t))
    a.a_binders;
  with_subst a.a_body_subst (fun () ->
      Option.iter (fun g -> pf ft "%a = true →@ " (expr ctx) g) c.guard);
  let spec_args =
    with_subst
      (List.map (fun (x, (t, _)) -> (x, t)) a.a_subst)
      (fun () ->
        List.map
          (fun (x, _) -> Fmt.str "%a" (expr ctx) { c.body with e = EVar x })
          f.params)
  in
  pf ft "Refines FS (%s.spec %s)@ (%a)@]@ @ " f.name
    (String.concat " " spec_args)
    (fun ft () -> with_subst a.a_body_subst (fun () -> expr ctx ft c.body))
    ()

let statements ~sources ft (p : program) =
  let ctx = classify p in
  header ~sources ft [ "Bvr.Semantics" ];
  pf ft
    "@[<v 2>/-- Every rule function refines its spec. -/@ structure Ops.Sound \
     (FS : FloatSem) (O : Ops) : Prop where@ orc : O.orc.Compat FS";
  List.iter
    (fun f ->
      pf ft "@ %s : ∀ %a, Refines FS (%s.spec %a) (O.%s %a)" f.name params f
        f.name args f f.name args f)
    (rule_fns ctx);
  pf ft "@]@ @ ";
  List.iter
    (fun f ->
      List.iter
        (fun r ->
          let _, _, g = r in
          let n = id (rule_name f g) in
          pf ft
            "@[<v 2>def %s.r_%s.Stmt : Prop :=@ ∀ (FS : FloatSem) (O : Ops), \
             O.Sound FS →@ ∀ %a (res : Term), %s.r_%s O %a = some res →@ \
             Refines FS (%s.spec %a) res@]@ @ "
            f.name n params f f.name n args f f.name args f)
        (rules f);
      if f.cases then
        List.iter
          (fun (r, arms) ->
            List.iteri (fun i a -> arm_stmt ctx ft f r i a) arms)
          (arms f))
    (rule_fns ctx);
  pf ft "end Bvr@]@."

(** The user functions that [e] calls. *)
let rec calls (e : expr) =
  match e.e with
  | EVar _ | EInt _ | EBool _ | EUnit | ENone | ENil -> []
  | ECall (f, l) -> f :: List.concat_map calls l
  | EConstr (_, l) | ELocalCall (_, l) | ETuple l -> List.concat_map calls l
  | ENode (a, b) | EBinop (_, a, b) | ECons (a, b) | EAssert (a, b) ->
      calls a @ calls b
  | EUnop (_, a) | ESome a | EField (a, _) -> calls a
  | EIf (a, b, c) -> calls a @ calls b @ calls c
  | ERecord l -> List.concat_map (fun (_, e) -> calls e) l
  | ELet (_, a, b) | ELetFun (_, _, a, b) -> calls a @ calls b
  | EMatch (scruts, cases) ->
      List.concat_map calls scruts
      @ List.concat_map
          (fun (c : case) ->
            Option.fold ~none:[] ~some:calls c.guard @ calls c.body)
          cases

(** One lemma per rule function: its spec is monotone in its term arguments, so
    a call of the function on terms that refine others refines the spec on
    those. *)
let lifts ~sources ft (p : program) =
  let ctx = classify p in
  header ~sources ft [ "Bvr.Lib.Lift" ];
  pf ft "namespace Lib@ @ variable {FS : FloatSem} {O : Ops}@ @ ";
  List.iter
    (fun f ->
      let term (_, t) = t = TTerm in
      let prime (x, t) = if term (x, t) then id x ^ "'" else id x in
      let helpers =
        List.filter
          (fun g -> List.exists (fun (h : fn) -> h.name = g) ctx.fns)
          (calls (Option.get f.spec))
        |> List.sort_uniq compare
      in
      pf ft "@[<v 2>theorem lift_%s (hO : O.Sound FS)" f.name;
      List.iter
        (fun (x, t) ->
          if term (x, t) then pf ft " {%s %s' : Term}" (id x) (id x)
          else pf ft " {%s : %a}" (id x) lean_ty t)
        f.params;
      List.iter
        (fun (x, t) ->
          if term (x, t) then
            pf ft "@ (h_%s : Refines FS %s %s')" x (id x) (id x))
        f.params;
      pf ft " :@ Refines FS (%s.spec %s) (O.%s %s) :=@ " f.name
        (String.concat " " (List.map (fun (x, _) -> id x) f.params))
        f.name
        (String.concat " " (List.map prime f.params));
      if List.exists term f.params then
        pf ft "Refines.trans (by simp only [%s]; bvr_congr) (hO.%s %s)@]@ @ "
          (String.concat ", " ("bvr_spec" :: helpers))
          f.name
          (String.concat " " (List.map prime f.params))
      else
        pf ft "hO.%s %s@]@ @ " f.name
          (String.concat " " (List.map prime f.params)))
    (rule_fns ctx);
  pf ft "end Lib@ @ end Bvr@]@."

(** The proofs of the rules of a [[@cases]] function from those of its arms, and
    of the arms derived by commutativity. *)
let cases_proofs ft (f : fn) =
  List.iter
    (fun (r, arms) ->
      List.iteri
        (fun i a ->
          match derived_from arms a with
          | None -> ()
          | Some b ->
              let j =
                let rec find k = function
                  | x :: _ when x == b -> k
                  | _ :: l -> find (k + 1) l
                  | [] -> assert false
                in
                find 0 arms
              in
              let hg = if a.a_case.guard = None then "" else " hg" in
              pf ft
                "@[<v 2>theorem %s.proof : %s.Stmt := by@ intro FS O hO %a%s@ \
                 exact Refines.trans@   (by simp only [%s.spec, ty, \
                 Term.ty_mk]; bvr_comm)@   (%s.proof FS O hO %a%s)@]@ @ "
                (arm_name f r i) (arm_name f r i)
                (list ~sep:" " (fun ft (x, _) -> pf ft "%s" x))
                a.a_binders hg f.name (arm_name f r j)
                (list ~sep:" " (fun ft (x, _) -> pf ft "%s" x))
                b.a_binders hg)
        arms;
      (* the alternatives come out of [repeat' rcases] in order *)
      pf ft
        "@[<v 2>theorem %s.r_%s.proof : %s.r_%s.Stmt := by@ intro FS O hO %a \
         res h@ simp only [%s.r_%s] at h@ repeat' rcases Lib.orElse_some h \
         with h | h@ %a@]@ @ "
        f.name (id r) f.name (id r) args f f.name (id r)
        (Format.pp_print_list
           ~pp_sep:(fun ft () -> pf ft "@ ")
           (fun ft i -> pf ft "· bvr_arm h (%s.proof FS O hO)" (arm_name f r i)))
        (List.init (List.length arms) Fun.id))
    (arms f)

let soundness ~sources ~proofs ft (p : program) =
  let ctx = classify p in
  let proofs =
    if List.exists (fun f -> f.cases) (rule_fns ctx) then
      "Bvr.Lib.Cases" :: proofs
    else proofs
  in
  header ~sources ft ("Bvr.Statements" :: proofs);
  List.iter (fun f -> if f.cases then cases_proofs ft f) (rule_fns ctx);
  List.iter
    (fun f ->
      pf ft
        "@[<v 2>theorem %s.step_sound (FS : FloatSem) (O : Ops) (hO : O.Sound \
         FS) %a :@ Refines FS (%s.spec %a) (%s.step O %a) := by@ unfold \
         %s.step@ "
        f.name params f f.name args f f.name args f f.name;
      List.iter
        (fun r ->
          let _, _, g = r in
          let n = id (rule_name f g) in
          pf ft
            "refine Refines.firstSome_cons (fun res h => %s.r_%s.proof FS O hO \
             %a res h) ?_@ "
            f.name n args f)
        (rules f);
      pf ft "exact Refines.firstSome_nil@]@ @ ")
    (rule_fns ctx);
  pf ft
    "@[<v 2>/-- Every rule function refines its spec, for any amount of fuel. \
     -/@ theorem opsN_sound (FS : FloatSem) (orc : Oracle) (h : orc.Compat FS) \
     :@ ∀ n, (opsN orc n).Sound FS@ | 0 =>\n\
    \    { orc := h";
  List.iter
    (fun f -> pf ft ",\n      %s := fun %a => Refines.refl" f.name args f)
    (rule_fns ctx);
  pf ft
    " }\n\
    \  | n + 1 =>\n\
    \    have hO := opsN_sound FS orc h n\n\
    \    { orc := hO.orc";
  List.iter
    (fun f -> pf ft ",\n      %s := %s.step_sound FS _ hO" f.name f.name)
    (rule_fns ctx);
  pf ft " }@]@ @ end Bvr@]@."
